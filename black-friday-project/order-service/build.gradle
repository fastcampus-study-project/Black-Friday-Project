plugins {
	id 'com.google.protobuf' version '0.9.4'
	id 'jacoco'
}

dependencies {
	// Spring Web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	// Spring Data JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// Spring Cloud OpenFeign
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	// H2 Database
	implementation 'com.h2database:h2'
	// MySQL
	runtimeOnly 'com.mysql:mysql-connector-j'
	// Kafka
	implementation 'org.springframework.kafka:spring-kafka'
	// Protobuf
	implementation 'com.google.protobuf:protobuf-java:3.25.2'
	// Test Kafka
	testImplementation 'org.springframework.kafka:spring-kafka-test'
}

test {
    useJUnitPlatform()

	finalizedBy jacocoTestReport
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.25.2'
	}
}

// jacocoTestReport 대상에서 제외시킬 경로 지정
private excludedClassFilesForReport(classDirectories) {
	classDirectories.setFrom(
			files(classDirectories.files.collect {
				fileTree(dir: it, exclude: [
						"**/protobuf/**",
						"**/*Application*",
						"**/*Config*"
				])
			})
	)
}

jacocoTestReport {
	dependsOn test	// 이 태스크 실행 전에 test 실행
	finalizedBy jacocoTestCoverageVerification	// 이 태스크 실행 후에 jacocoTestCoverageVerification 실행

	excludedClassFilesForReport(classDirectories)	// 제외 경로 설정
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'BUNDLE'	// 프로젝트 단위로

			limit {
				counter = 'LINE'		// 라인을 기준으로
				value = 'COVEREDRATIO'	// 비율
				minimum = 0.89			// 커버리지 89%를 최소 기준으로 잡음
			}

			includes = [	// 검증에 포함시킬 경로들
				'com.fastcampus.order_service.service.**',
			]
		}
	}
}
